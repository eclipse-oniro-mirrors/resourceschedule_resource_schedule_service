# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import(
    "//foundation/resourceschedule/resource_schedule_service/cgroup_sched/cgroup_sched.gni")
import(
    "//foundation/resourceschedule/resource_schedule_service/ressched/ressched.gni")
import(
    "//foundation/resourceschedule/resource_schedule_service/ressched/sched_controller/sched_controller.gni")

ohos_shared_library("sched_controller") {
  include_dirs = [
    "common_event/include",
    "observer/include",
    "//base/hiviewdfx/hisysevent/interfaces/native/innerkits/hisysevent_manager/include",
    "//foundation/bundlemanager/bundle_framework/interfaces/inner_api/appexecfwk_core/include/bundlemgr",
    "//foundation/resourceschedule/device_scheduler/common/include",
    "//foundation/resourceschedule/resource_schedule_service/ressched/common/include",
    "//base/hiviewdfx/hisysevent/interfaces/native/innerkits/hisysevent/include",
    "${cgroup_sched_framework}/process_group/include",
    "${cgroup_sched_framework}/sched_controller/include",
    "${cgroup_sched_framework}/utils/include",
    "${cgroup_sched_common}/include",
    "${ressched_common}/include",
    "${ressched_interfaces}/innerkits/ressched_client/include",
  ]

  sources = [
    "${cgroup_sched_common}/cgroup_sched_log.cpp",
    "${cgroup_sched_framework}/utils/ressched_utils.cpp",
    "common_event/src/event_controller.cpp",
    "observer/src/audio_observer.cpp",
    "observer/src/camera_observer.cpp",
    "observer/src/observer_manager.cpp",
  ]

  external_deps = [
    "ability_base:want",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "hisysevent_native:libhisyseventmanager",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "multimedia_audio_framework:audio_client",
    "samgr:samgr_proxy",
  ]

  defines = []

  configs = []

  if (device_movement_perception_enable) {
    configs += [ "//base/msdp/movement/utils:movement_utils_config" ]
    defines += [ "DEVICE_MOVEMENT_PERCEPTION_ENABLE" ]
    external_deps += [ "movement:movement_client" ]
    sources += [ "observer/src/device_movement_observer.cpp" ]
  }

  if (ressched_with_telephony_state_registry_enable) {
    defines += [ "RESSCHED_TELEPHONY_STATE_REGISTRY_ENABLE" ]
    include_dirs +=
        [ "//base/telephony/state_registry/frameworks/native/observer/include" ]
    include_dirs +=
        [ "//base/telephony/core_service/interfaces/innerkits/include" ]
    external_deps += [ "state_registry:tel_state_registry_api" ]
    sources += [ "observer/src/sched_telephony_observer.cpp" ]
  }

  part_name = "resource_schedule_service"
  subsystem_name = "resourceschedule"
}
