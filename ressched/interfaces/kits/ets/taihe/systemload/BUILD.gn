# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

subsystem_name = "resourceschedule"
part_name = "resource_schedule_service"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/systemload"

copy_taihe_idl("copy_systemload_taihe") {
  sources = [ "idl/ohos.resourceschedule.systemload.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_systemload_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.resourceschedule.systemload.ani.cpp",
    "$taihe_generated_file_path/src/ohos.resourceschedule.systemload.abi.c",
  ]
}

taihe_shared_library("systemload_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "../../../../../common/include" ] 
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.resourceschedule.systemload.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "../../../../innerkits/ressched_client:ressched_client",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]
}

generate_static_abc("systemload_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.resourceschedule.systemload.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/systemload_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("systemload_etc") {
  source = "$target_out_dir/systemload_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":systemload_abc" ]
}

group("systemload_taihe") {
  deps = [
    ":systemload_taihe_native",
    ":systemload_etc",
  ]
}